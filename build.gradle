buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

wrapper {
    gradleVersion = '5.1'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = '0.8.4'
    }

}

subprojects {
    repositories {
        jcenter()
    }

    group 'it.at7.gemini'
    version '0.2.x'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9

    dependencies {
        compile 'org.jetbrains:annotations:17.0.0'
        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile 'org.springframework.security:spring-security-test'
    }

    def jacocoExecutionDataFiles = fileTree(buildDir).include("/jacoco/*.exec")
    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled = true
        }
        executionData.setFrom(jacocoExecutionDataFiles)
    }

}

task jacocoRootReport(type: JacocoReport) {
    dependsOn subprojects.jacocoTestReport
    reports {
        xml.enabled = true
    }
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom files(subprojects.jacocoTestReport.executionData)
}